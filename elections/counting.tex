\section{Counting}

\subsection{Functional Paradigm Pseudocode for the STV Algorithm}

The pseudocode given in the previous section describes, in an imperative sense,
what implementing the single transferable vote would be like. For the purposes
of implementing STV in a functional programming language, it is necessary for us
to translate such an algorithm into the functional style. Such pseudocode is
given in Algorithm~\ref{functional_stv}. In this style, we are making a judgment
on each candidate, declaring them either to be elected or unelected. We recurse
on our list of candidates, appending a list of judged candidates we are building
with either judgments of \texttt{Elected} or \texttt{Eliminated}. We continue
until we hit our base case of an empty candidates list, in which case we return
the empty list. This is one way with which we can implement a functional STV
counter. Of course, the lower-level detail including data structures and types
are provided in the rest of this section. This pseudocode should be used to get
a general sense of how a functional-paradigm STV counter is structured but I
don't expect a reader to dwell on it for too long. 

\begin{algorithm}
	\SetKwFunction{stv}{stv}
	\SetKwProg{Fn}{Function}{}{}
	\Fn{\stv{ballots, seats, candidates}}{
        \eIf{candidates is not empty}{
            // countBallots functions declared in imperative pseudocode.\\
            countedBallots = countBallots(candidates, ballots)\\
            highestCandidate = candidate with highest score\\
			\eIf{highestCandidate >= droopQuota and seats > 0}{
                newCandidates = candidates.remove(highestCandidate)\\
                mark highestCandidate as ELECTED\\
                // transferDown functions similarly to the imperative pseudocode\\
                newBallots = transferDown(countedBallots, highestCandidate)\\
                \Return{prepend(highestCandidate, stv(newBallots, seats, newCandidates))}
			}{
                lowestCandidate = candidate with the lowest score. \\
                newCandidates = candidates.remove(lowestCandidate)\\
                mark lowestCandidate as ELIMINATED\\
                // transferUp functions similarly to the imperative pseudocode\\
                newBallots = transferUp(countedBallots, lowestCandidate)\\
                \Return{prepend(lowestCandidate, stv(newBallots, seats, newCandidates))}
            }
        }{
            \Return{empty list}
        }
    }
    \caption{Functional paradigm pseudo-code for Palpatine.}
    \label{functional_stv}
\end{algorithm}

\subsection{High-Level Overview of STV Code} \label{highlevelstv}

To implement STV, we choose to iterate through a list of candidates, and make
judgments on each candidate. In other words, rather than build up a list of
candidates, we simply iterate through our candidates, assigning them a judgment
of either elected or unelected. Our overall code (ignore the types for the time
being), ends up looking like in Figure~\ref{overall_code}. Just simply think of
\texttt{Election} as a type where its first natural number parameter refers to
the number of remaining candidates left and the second natural number refers to
the number of candidates that have been judged.

In essence, our \texttt{stv} function repeatedly iterates over our
\texttt{Election}, calling \texttt{processOne}. \texttt{processOne}
checks to see if the highest scoring candidate is above the droopQuota. If the
candidate is above the droopQuota, we elect the candidate. If not, we eliminate
a candidate. If there are no seats left, as shown in the second parameter of the
first pattern match, we run \texttt{notElectedHead}, which just eliminates one
candidate. If there are no seats left, \texttt{notElectedHead} is called
repeatedly until we eliminate all the candidates. For the time being, we can
ignore the use of \texttt{rewrite} in the last line of the code. 

\begin{figure}[htbp!!!!!!!!]
	\caption{Ballots with initial score.}
	\label{overall_code}
	\begin{lstlisting}
        total
        processOne : Election (S r) j -> Election r (S j)
        processOne election@(_, Z, _, _, _)          = notElectedHead election
        processOne election@(dq, (S n), _, _, _) = case count election of
            counted@(_, _, _, cands, _) => if weCanElect dq cands
                then electOne counted
                else elimOne counted

        ||| Running an STV election involves taking in the candidates, the seats, the
        ||| ballots and producing a list of candidates to take the seats. 
        ||| Returns a tuple of elected candidates and unelected candidates.
        total
        stv : Election r j -> Election Z (r + j)
        stv         e@(_, _, _, Nil, _)      = e
        stv {r=(S n)} {j} e@(_, _, _, (_ :: _), _) = 
            rewrite plusSuccRightSucc n j in stv $ processOne e
    \end{lstlisting}
\end{figure}

\subsection{Data Structures}

We need to store several pieces of information. This section describes the data
structures we will be using in our STV vote counter.

\begin{enumerate}
	\item We need to store the candidates remaining and also the score of each
	      candidate.
	\item We need to store a series of ballots. As we revalue ballots, we need
	      to revalue the ballots based on what each one of the ballots has voted for
	      when a given candidate is elected. In other words, each ballot contains
	      unexhausted preferences for candidates, candidates that the ballot has voted
	      for, and the value of the ballot.
	\item We need to store the judgments. In other words, we need to store a
	      growing list of each candidate that contains the judgment that the candidate
	      was either elected or not elected.
	\item We need to store the seats remaining. This is stored as a natural
    number. 
    \item We need to store the Droop Quota. This is stored as an integer. 
\end{enumerate}

\subsubsection{Candidates Data Type}

We choose to represent each candidate as a record type\footnote{Record types are
present in Haskell so they are assumed as background knowledge. As always, Real
World Haskell serves as a good primer for background Haskell knowledge and
discusses record syntax in Chapter 3. A link is available here:
http://book.realworldhaskell.org/read/defining-types-streamlining-functions.html}.
All the code relevant to candidates is shown in Figure~\ref{candidates_code}.
While we have generic getters as well as incrementors and decrementors for the
VoteValue of a candidate, we also have a function \texttt{getHighestIndex} that
takes in an element of type \texttt{Candidates (S n)} and returns the index of
the candidate with the highest score. This index is represented by a number of
type \texttt{Fin (S n)}. \texttt{Fin (S n)} is a type that represents the set of
numbers between zero and the natural number \texttt{S n}. In other words, this
type signature guarantees to us that the index we return will be within bounds
of our length-indexed vector.

\begin{figure}[htbp!!!!!!!!!!!!!!]
	\caption{Candidates data type with relevant accessors}
	\label{candidates_code}
	\begin{lstlisting}
        CandidateName : Type
        CandidateName = String

        VoteValue : Type
        VoteValue = Double

        record Candidate where
            constructor MkCandidate
            candName : CandidateName
            candValue : VoteValue
        
        Candidates : Nat -> Type
        Candidates n = Vect n Candidate

        ||| Returns the index and score of the candidate with the highest score. 
        getHighestIndex : Candidates (S n) -> (Fin (S n), VoteValue)

        ||| Returns the index and score of the candidate with the lowest score. 
        getLowestIndex : Candidates (S n) -> (Fin (S n), VoteValue)

        ||| Adds a votevalue to the score of a candidate. 
        addVoteVal : Fin n -> Candidates n -> VoteValue -> Candidates n
        
        ||| Decrements a votevalue from the score of a candidate. 
        decVoteVal : Fin n -> Candidates n -> VoteValue -> Candidates n

        ||| Removes a candidate from the vector of candidates. 
        removeCand : Fin (S n) -> Candidates (S n) -> Candidates n

        ||| Makes a new candidate with score 0. 
        makeNewCandidate : CandidateName -> Candidate
    \end{lstlisting}
\end{figure}

\subsubsection{Storing results}

Like \texttt{Candidate}, we also choose to represent each judgment as a record
type \texttt{Judged}. A value of type \texttt{Judged} consists of a candidate
name and whether or not that candidate was elected. To represent that, we must
declare a new algebraic data type called \texttt{Judgment}. \texttt{Judgment}
simply contains two values and is in a way analogous to \texttt{Bool}. It simply
tells us if a \texttt{candidate} is \texttt{Elected} or \texttt{NotElected}. We
then have a \texttt{Results} data type that is a length-indexed vector
containing elements of type \texttt{Judged}. 

\begin{figure}[htbp!!!!!!!!!!!!!!]
	\caption{Results data type with relevant accessors}
	\label{judgments_code}
    \begin{lstlisting}
        record Judged where
            constructor MkJudgment
            candName : CandidateName
            judgment : Judgment

        data Judgment = Elected | NotElected

        Results : Nat -> Type
        Results n = Vect n Judged
        
        emptyResults : Results Z
        emptyResults = Nil

        elect : Candidate -> Judged
        elect cand = MkJudgment (candName cand) Elected

        dontElect : Candidate -> Judged
        dontElect cand = MkJudgment (candName cand) NotElected
    \end{lstlisting}
\end{figure}

\subsubsection{Storing Ballots}


\begin{figure}[htbp!!!!!!!!!!!!!!]
	\caption{Ballots data type with relevant accessors and setters}
	\label{ballots_code}
    \begin{lstlisting}
        Preferences : Nat -> Type
            Preferences n = List $ Fin n

        record Ballot (n : Nat) where
            constructor MkBallot
            votedFor : List CandidateName
            prefs : Preferences n
            value : VoteValue
        
        Ballots : Nat -> Type
        Ballots n = List $ Ballot n

        ||| nextCand returns the index of the ballot's next preference if one exists. 
        total
        nextCand : Ballot n -> Maybe $ Fin n
        nextCand ballot         = case getPrefs ballot of
            [] => Nothing
            (cand :: _) => Just cand

        replacePrefs : Preferences n -> Ballot n -> Ballot n
        replacePrefs new ballot = record { prefs = new } ballot

        ||| restCand returns the rest of the ballot's preferences. 
        total
        restCand : Ballot n -> Ballot n
        restCand ballot         = case getPrefs ballot of
            []          => ballot
            (_ :: rest) => replacePrefs rest ballot

        ||| takes a CandidateName and checks to see if the ballot has 
        ||| voted for the candidate. 
        ballotDidElectCandidate : CandidateName -> Ballot n -> Bool
        ballotDidElectCandidate name ballot = elem name (votedFor ballot)
        
        ||| adds the next candidate preferenced to the
        ||| list of candidates that this ballot has voted for. 
        addToVotedFor : Candidates n -> Ballot n -> Ballot n
        addToVotedFor cands ballot = case nextCand ballot of
            Just topPrefIndex => let name = candName (index topPrefIndex cands) in 
                record { 
                    votedFor = name :: (votedFor ballot) 
                } (restCand ballot)
            Nothing => ballot
        ||| check to see if our ballot has voted for the candidate and 
        ||| if it has, we change its weight to the new weight given. 
        total
        changeBallotIfIsCand : CandidateName -> VoteValue -> Ballot n -> Ballot n
        changeBallotIfIsCand cand vv ballot = if ballotDidElectCandidate cand ballot
            then replaceValue vv ballot
            else ballot
    \end{lstlisting}
\end{figure}


A ballot contains, as described above, three things. A list of unexhausted
preferences, representing candidates the ballot has yet to vote for, a value,
denoting the current score of the ballot, and a list of candidates that the
ballot has already voted for. This can be represented as a record type.
\texttt{Preferences n} is a type that takes in a natural number representing the
number of candidates left in \texttt{Candidates n}. Each \texttt{Fin} it
contains is bounded by the index of the candidates remaining. In other words,
each \texttt{Fin} stored as a \texttt{Preference} maps to an element in
\texttt{Candidates n}. The code for storing and handling ballots is given in
Figure~\ref{ballots_code}. Functions that act on ballots are also provided in
the figure, with comments explaining their purpose. As each preference is counted
for a ballot, we index the candidate name from the \texttt{Candidates n}
length-indexed vector and we take that candidate and we put it into
\texttt{votedFor}, representing the list of candidates the ballot has voted for.


\subsubsection{The Election Type}

Now we can group together all the different data structures we've built to make
the \texttt{Election} data type, which contains the Droop Quota as an
\texttt{Integer}, the number of seats available as a natural number, the
ballots, the candidates and the results. The code for the \texttt{Election} type
is given in Figure~\ref{election_type_code}. 

\begin{figure}[ht!!!!!!!!!!!!!!]
	\caption{Election Type}
	\label{election_type_code}
	\begin{lstlisting}
        Election : Nat -> Nat -> Type
        Election r j = (Int, Nat, Ballots r, Candidates r, Results j)

        makeElection : Int 
                    -> Nat 
                    -> Ballots r
                    -> Candidates r 
                    -> Results j 
                    -> Election r j
        makeElection i n b r j = (i, n, b, r, j)
    \end{lstlisting}
\end{figure}

\newpage

\subsection{Application of Data Structures to STV}

Having established our data structures, we now move back to the high-level STV
code we introduced earlier in Section~\ref{highlevelstv}. The code in
Figure~\ref{overall_code} has been reproduced in Figure~\ref{overall_code2} for
the sake of convenience.

\begin{figure}[ht!!!!!!!!]
	\caption{Reproduced overall STV code.}
	\label{overall_code2}
	\begin{lstlisting}
        total
        processOne : Election (S r) j -> Election r (S j)
        processOne election@(_, Z, _, _, _)          = notElectedHead election
        processOne election@(dq, (S n), _, _, _) = case count election of
            counted@(_, _, _, cands, _) => if weCanElect dq cands
                then electOne counted
                else elimOne counted

        ||| Running an STV election involves taking in the candidates, the seats, the
        ||| ballots and producing a list of candidates to take the seats. 
        ||| Returns a tuple of elected candidates and unelected candidates.
        total
        stv : Election r j -> Election Z (r + j)
        stv         e@(_, _, _, Nil, _)      = e
        stv {r=(S n)} {j} e@(_, _, _, (_ :: _), _) = 
            rewrite plusSuccRightSucc n j in stv $ processOne e
    \end{lstlisting}
\end{figure}

Now, notice that we're doing type-level arithmetic on the natural numbers in our
\texttt{Election} data type. Our \texttt{stv} function takes in a value of type
\texttt{Election} with $r$ remaining candidates and $j$ made judgments. Our
function is guaranteed to return an element of type \texttt{Election} with no
remaining candidates $Z$ and judgments on all the candidates $r+j$. As mentioned
above, our \texttt{stv} function calls \texttt{processOne} repeatedly. Now we
also have guarantees that \texttt{processOne} takes in an \texttt{Election} with
at least one candidate remaining. Notice that the first natural number of
\texttt{Election} is \texttt{(S r)}, so it must be of one or greater since it is
not Zero, or $Z$. 

What's left then, is to introduce several functions that were mentioned in the
\texttt{stv} and \texttt{processOne} functions. We must first introduce the
function for counting the first preferences of each ballot. We must then
introduce the function for electing a candidate and eliminating a candidate. We
must then introduce the predicate \texttt{weCanElect}. We must also introduce
\texttt{notElectedHead} which just eliminates a candidate indiscriminately after
all the seats have been filled. Finally, we will comment on the use of
\texttt{rewrite} in the last line of \texttt{stv}. 

\subsubsection{Counting First Preferences}

\begin{figure}[ht!!!!!!!!]
	\caption{Code for counting the first preference of each ballot.}
	\label{counting_code}
    \begin{lstlisting}
        ||| Does a full count of the ballots by taking the top preference and the vote
        ||| value and putting it into the candidates votes for the given candidate. 
        total
        count : Election r j -> Election r j
        count {r} election@(dq, seats, ballots, cands, results) = 
            makeElection dq seats newBallots newCands results
                -- For the sake of brevity, the code for deriving newBallots 
                -- and newCands has been omitted. 
    \end{lstlisting}
\end{figure}

Counting ballots does not cause a change in the type of our \texttt{Election} in
any way. Therefore, we will gloss over counting with a quick description as to
keep the focus of this work on types and verification rather than
implementation. A call to \texttt{count} does a full count of our ballots. For
each ballot in \texttt{ballots}, we take its first preference as an index to a
candidate in the \texttt{cands} length-indexed vector of candidates. We then add
the ballot's weight to that candidate in \texttt{cands}. Finally, we remove that
ballot's first preference and add the candidate it just voted for to its
\texttt{votedFor} list. The aggregate produced new length-indexed vector of
candidates becomes \texttt{newCands}, and the aggregate produced new ballots
become \texttt{newBallots}. We build another \texttt{Election} with the same
number of candidates remaining and judged as none have been elected or
eliminated in this process. 

\subsubsection{Removing candidates once all have been elected}

\begin{figure}[ht!!!!!!!!]
	\caption{Code for when we are just eliminating candidates indiscriminately.}
	\label{not_elected_head_code}
    \begin{lstlisting}
        ||| After a candidate was just eliminated from remaining, this iterates through
        ||| the ballots and reindexes them according to the new candidate indices rather
        ||| than the old one. 
        total
        reindexBallots : Ballots (S r) -> Candidates (S r) -> Candidates r -> Ballots r
        reindexBallots {r} ballots oldCands newCands = 
            map reindexBallot ballots where
                reindexCand : Fin $ S r -> Maybe $ Fin r
                reindexCand oldPref = findIndex (\x => candName x == cn) newCands where
                    cn : CandidateName
                    cn = candName $ index oldPref oldCands
                reindexBallot : Ballot (S r) -> Ballot r
                reindexBallot ballot = newBallot where
                    newPrefs : Preferences r
                    newPrefs = mapMaybe reindexCand $ getPrefs ballot
                    newBallot : Ballot r
                    newBallot = MkBallot (votedFor ballot) newPrefs (value ballot)

        total
        notElectedHead : Election (S r) j -> Election r (S j)
        notElectedHead election@(dq, seats, ballots, (x :: xs), results) = 
            makeElection 
                dq 
                seats 
                (reindexBallots ballots (x :: xs) xs)
                xs
                ((dontElect x) :: results)
    \end{lstlisting}
\end{figure}

Notice how in Figure~\ref{overall_code2} that when \texttt{processOne} sees that
there are no seats left, it calls a function called \texttt{notElectedHead}.
\texttt{notElectedHead} simply makes a judgment that the next candidate has
failed to be elected. The code for this function is provided in
Figure~\ref{not_elected_head_code}. It takes in the existing election and it does
three things: 

\begin{enumerate}
    \item It removes the head from the length-indexed vector of remaining candidates
    \item It adds the head with the judgment of not elected to the vector of judged candidates. 
    \item It reindexes the ballots, so that the preferences are now a list of
    indices that correspond to the new length-indexed vector of candidates with
    the head candidate removed. 
\end{enumerate}

Most of this code does not need to be explained since it just involves pattern
matching on and manipulating the head and rest of a length-indexed vector. What
deserves comment is the presence of the function \texttt{reindexBallots}.
\texttt{reindexBallots} will be used elsewhere in our election and elimination
functions as well. As described above, it takes in \texttt{Ballots}, each
indexed according to a previous, larger length-indexed vector of remaining
candidates. It then indexes them according to the next, smaller length-indexed
vector of candidates. This function does a \texttt{map} over the ballots,
calling the function \texttt{reindexBallot} on each function.
\texttt{reindexBallot} does a mapMaybe of \texttt{reindexCand}, grabbing each
preference from the ballot and seeing whether it is present in the new
length-indexed vector of remaining candidates. \texttt{mapMaybe} is a
\texttt{map} function that does not append to a list if the result is
\texttt{Nothing}. In other words, if the candidate being indexed is not in the
new length-indexed vector, it won't be part of the returned list of preferences
in the ballot. 

\subsubsection{Eliminating a candidate}

\begin{figure}[ht!!!!!!!!]
	\caption{Code for eliminating a candidate}
	\label{elim_cand_code}
    \begin{lstlisting}
        ||| elimOne eliminates a candidate. It chooses the lowest valued candidate
        ||| and then makes a judgment that the candidate is eliminated. It then
        ||| reindexes the ballots without changing the vote value of the ballots. 
        total
        elimOne : Election (S r) j -> Election r (S j)
        elimOne {r} {j} election@(dq, seats, ballots, cands, results) = 
            (dq, seats, newBallots, newCands, newResults) where
                lowestCandIndex : Fin $ S r
                lowestCandIndex = case getLowestIndex cands of
                    (i, _) => i
                result : Judged
                result = dontElect $ getCand lowestCandIndex cands
                newResults : Results (S j)
                newResults = (result :: results)
                newCands : Candidates r
                newCands = removeCand lowestCandIndex cands
                newBallots : Ballots r
                newBallots = reindexBallots ballots cands newCands
    \end{lstlisting}
\end{figure}

When we eliminate a candidate, the first thing we do is we get the index of the
candidate with the lowest score. We then make a judgment of this candidate as a
candidate we are not going to elect by calling our \texttt{dontElect} function
to create an element of type \texttt{Judged}. We then add this to our
length-indexed vector of judgments. Whereas previously this length-indexed
vector was of length \texttt{j}, it is now of length \texttt{(S j)}. We remove that
candidate from our length-indexed vector of candidates. Whereas previously our
length-indexed vector of remaining candidates was of length \texttt{(S r)},
having just eliminated the candidate with the lowest score, it now has length
\texttt{r}. We reindex the candidates according to our new length-indexed vector
of remaining candidates and we rebuild our election. This election now has type
\texttt{Election r (S j)}, indicating that we have successfully judged one
candidate in the process. 

\subsubsection{Electing a candidate}

\begin{figure}[ht!!!!!!!!]
	\caption{Code for electing a candidate.}
	\label{elect_cand_code}
    \begin{lstlisting}
        ||| electOne elects a candidate. It takes in the highest candidate index and
        ||| makes a judgment on that candidate as the new elected candidate. It then
        ||| redistributes the preferences according to the new transfer value. 
        total
        electOne : Election (S r) j -> Election r (S j)
        electOne {r} {j} election@(dq, seats, ballots, cands, results) = 
            (dq, seats, newBallots, newCands, newResults) where
                highestCandIndex : Fin $ S r
                highestCandIndex = case getHighestIndex cands of
                    (i, _) => i
                highestCand : CandidateName
                highestCand = candName $ index highestCandIndex cands
                highestCandValue : VoteValue
                highestCandValue = candValue $ index highestCandIndex cands
                result : Judged
                result = elect $ getCand highestCandIndex cands
                newResults : Results (S j)
                newResults = (result :: results)
                newCands : Candidates r
                newCands = removeCand highestCandIndex cands
                newBallotVal : VoteValue
                newBallotVal = transferValue dq highestCandValue
                -- This creates ballots with newBallotVal (if they preferenced highest
                -- cand first). 
                ballotsWithNewValue : Ballots (S r)
                ballotsWithNewValue = 
                    map (changeBallotIfIsCand highestCand newBallotVal) ballots
                newBallots : Ballots r
                newBallots = reindexBallots ballotsWithNewValue cands newCands
    \end{lstlisting}
\end{figure}

Electing a candidate works in similar ways to eliminating one. We start by
getting the highest index. We then get the candidate name and value. We then
build a judgment of type \texttt{Judged} that declares our candidate to be
elected. After appending our new judgment to the length-indexed vector of
judgments, we remove the highest candidate from the length-indexed vector of
remaining candidates. We then calculate the transfer value, and then run through
all the ballots, changing their value if they voted for the candidate. Finally,
we reindex the ballots according to the newly created, one size smaller
length-indexed vector of remaining candidates. We then rebuild our election, now
of type \texttt{Election r (S j)}

\subsubsection{Why use rewrite?}


\begin{figure}[ht!!!!!!!!]
	\caption{STV code without rewrite}
	\label{no_rewrite_code}
    \begin{lstlisting}
        total
        stv : Election r j -> Election Z (r + j)
        stv         e@(_, _, _, Nil, _) = e
        stv e@(_, _, _, (_ :: _), _)    = stv $ processOne e
    \end{lstlisting}
\end{figure}

\begin{figure}[ht!!!!!!!!]
	\caption{Error message produced by Idris compiler.}
	\label{idris_error_code}
    \begin{lstlisting}
        When checking right hand side of stv with expected type
                Election 0 (S len + j)

        Type mismatch between
                Election 0 (len + S j) (Type of stv (processOne e))
        and
                (Int,
                 Nat,
                 List (Ballot 0),
                 Vect 0 Candidate,
                 Vect (S (plus len j)) Judged) (Expected type)

        Specifically:
                Type mismatch between
                        plus len (S j)
                and
                        S (plus len j)
    \end{lstlisting}
\end{figure}

The last item to explain in the code of our STV function is why we use
\texttt{rewrite}. If we didn't use \texttt{rewrite}, the code for \texttt{stv}
would look like the code given in Figure~\ref{no_rewrite_code}. The Idris
compiler produces an error, telling us that it is unable to compile. This error
message is reproduced in Figure~\ref{idris_error_code}.

What does this error message mean? It means that this function has an output of
\texttt{Election 0 (S len + j)}. However, the function makes a recursive call to
\texttt{processOne}, which produces an Election of type \texttt{Election len (S
j)}. The Idris compiler is unable to deduce that \texttt{(S len + j)} is the
same as \texttt{len + (S j)}. Luckily, there is a function in the Idris Prelude
that operates on natural numbers that proves this equality for us. It is called
\texttt{plusSuccRightSucc}. What we need to do is use this proof to rewrite some
of our type variables. \texttt{plusSuccRightSucc} has type signature:
\texttt{total plusSuccRightSucc : (left : Nat) -> (right : Nat) -> S (left +
right) = left + (S right)}. 

\begin{figure}[ht!!!!!!!!]
	\caption{STV code with the rewrite.}
	\label{with_rewrite_code}
	\begin{lstlisting}
        total
        stv : Election r j -> Election Z (r + j)
        stv         e@(_, _, _, Nil, _)      = e
        stv {r=(S len)} {j} e@(_, _, _, (_ :: _), _) = 
            rewrite plusSuccRightSucc len j in stv $ processOne e
    \end{lstlisting}
\end{figure}

How do we use the equality that's provided by \texttt{plusSuccRightSucc}? Idris
provides syntactic sugar for type coersion with a keyword called
\texttt{rewrite}. \texttt{rewrite} takes a proven equality and then whenever it
sees a type that is the same as one side of the equality, it will attempt
substituting that type with the type on the other side of the equality and see
if compilation would succeed with that type. 

Thus, to demonstrate the necessary equality, we need to call
\texttt{plusSuccRightSucc} with \texttt{len} and \texttt{j}, where \texttt{len}
refers to the natural number preceding \texttt{r} and \texttt{j} refers to the
number of judgments in the input \texttt{Election}. We then use the syntactic
sugar of \texttt{rewrite} to employ the proof of \texttt{plusSuccRightSucc}. The
subsequent type coersion rewrites the addition in order to demonstrate that our
recursive call to \texttt{stv \$ processOne} satisfies our type constraints. Our
code then successfully compiles. 

Thus, in this section, we have introduced all the code that Palpatine uses to
count STV ballots. 